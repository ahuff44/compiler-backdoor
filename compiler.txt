let RESERVED = _array("true", "false", "let", "def", "if", "while", "return", "break", "do", "raise", "alloc");
let STATEMENT_STARTERS = _array("let", "def", "if", "while", "return", "break", "do", "raise", "alloc");
let SYNTAX = _array("=", ";", "(", ")", ",", "{", "}", "<<");
let BINOPS = _array(   ".", "*", "+", "-", "==", "!=", "<", ">", "<=", ">=", "&&", "||");
let BINOP_PREC = _array(0,   10,  20,  20,  30,   30,   30,  30,  30,   30,   40,   50);
let ALL_SYMBOLS = SYNTAX.concat(BINOPS);

def is_symbol_character(char) {
  def is_relevant_symbol(sym) {
    return sym.includes(char);
  }
  return ALL_SYMBOLS.some(is_relevant_symbol);
}

let DIGITS = _array("0","1","2","3","4","5","6","7","8","9");
let LOWERCASE = _array("a","b","c","d","e","f","g","h","i","j","k","l","m","n","o","p","q","r","s","t","u","v","w","x","y","z");
let UPPERCASE = _array("A","B","C","D","E","F","G","H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Y","Z");

def tokenize_error(msg, pos) {
  let line = _leftpop(pos);
  let char = _leftpop(pos);
  do _print("Tokenization error at "+line+":"+char+":\n\t"+msg);
  do _EXIT(1);
}

def tokenize(text, logging) {
  alloc char_ix;
  alloc line_ix;
  alloc char_in_line;
  alloc char;
  let char_ix = 0; # 1-indexed
  let line_ix = 1; # 1-indexed
  let char_in_line = 0; # 1-indexed
  let char = "";
  let chars = text + "\n"; # extra char to allow tokens to complete. Necessary esp for comments - infinite loop otherwise
  def next_char_() {
    let char_ix = char_ix + 1;
    let char_in_line = char_in_line + 1;
    let char = _get(chars, char_ix - 1);
    if (char == "\n") {
      let line_ix = line_ix + 1;
      let char_in_line = 0; # 1-indexed
    }
  }
  def prev_char_(){
    let char_in_line = char_in_line - 1;
    if (char == "\n") {
      let line_ix = line_ix - 1;
      let char_in_line = -42; # @hack hard to know what it actually should be
    }
    let char_ix = char_ix - 1;
    if (char_ix < 0) {
      raise "iae: sub-0 char index";
    }
    let char = _get(chars, char_ix - 1);
  }

  def tokenize_heredoc(pos) {
    let target = _array();
    while (char == "_" || DIGITS.includes(char) || LOWERCASE.includes(char) || UPPERCASE.includes(char)) {
      do target.push(char);

      do next_char_();
    }
    if (char != "\n") {
      do tokenize_error("bad heredoc header; expected \\n, got \""+char+"\"", pos);
    }

    def array_eq(arr1, arr2) {
      if (arr1.length != arr2.length) {
        return false;
      }
      let ix = 0;
      while (ix < arr1.length) {
        if (_get(arr1, ix) != _get(arr2, ix)) {
          return false;
        }
        let ix = ix + 1;
      }
      return true;
    }

    let buffer = _array();
    while (false == array_eq(buffer.slice(-target.length), target)) {
      do buffer.push(char);

      do next_char_();
    }
    do prev_char_(); # put next char back

    return _array("STRING", buffer.join(""), pos);
  }

  let buffer = _array();
  let tokens = _array();

  do next_char_();
  while (char_ix < chars.length) {
    if (logging) {
      do _print(_array("tokenize", _array(char_ix, line_ix, char_in_line, char, peek(tokens))));
    }

    if (char == " " || char == "\t" || char == "\n") {
      # no-op
    } else if (char == "#") {
      do next_char_(); # consume

      while (char != "\n") {
        do next_char_();
      }
    } else if (char == "\"") {
      let pos = _array(line_ix, char_in_line);

      do next_char_(); # consume

      let buffer = _array();
      while (char != "\"") {
        if (char == "\\") {
          # escape char
          do next_char_();
          do buffer.push(generate_escape_code(char));
        } else {
          do buffer.push(char);
        }

        do next_char_();
      }

      do tokens.push(_array("STRING", buffer.join(""), pos));
    } else if (DIGITS.includes(char)) {
      let pos = _array(line_ix, char_in_line);

      let buffer = _array();
      while (DIGITS.includes(char)) {
        do buffer.push(char);

        do next_char_();
      }
      do prev_char_(); # put non \d char back

      do tokens.push(_array("INTEGER", buffer.join(""), pos));
    } else if (char == "_" || LOWERCASE.includes(char) || UPPERCASE.includes(char)) {
      let pos = _array(line_ix, char_in_line);

      let buffer = _array();
      while (char == "_" || DIGITS.includes(char) || LOWERCASE.includes(char) || UPPERCASE.includes(char)) {
        do buffer.push(char);

        do next_char_();
      }
      do prev_char_(); # put non-ident char back

      let val = buffer.join("");
      if (RESERVED.includes(val)) {
        do tokens.push(_array("RESERVED", val, pos));
      } else {
        do tokens.push(_array("IDENTIFIER", val, pos));
      }
    } else if (is_symbol_character(char)) {
      let pos = _array(line_ix, char_in_line);

      let buffer = _array();
      while (is_symbol_character(char)) {
        do buffer.push(char);

        if (_get(buffer, 0) == "<" && _get(buffer, 1) == "<" && buffer.length == 2){ # @hack the placement of the code is... awkward. the exit mechanism back into normal execution state is p weird too
          do next_char_();
          do tokens.push(tokenize_heredoc(pos));
          let buffer = _array();
        }

        do next_char_();
      }
      do prev_char_(); # put non-sym char back

      def validity_filter(sym) {
        # checks whether sym is a prefix of buffer
        let ix2 = 0;
        while (ix2 < sym.length) {
          let cbuf = _get(buffer, ix2);
          let sbuf = _get(sym, ix2);
          if (cbuf != sbuf) {
            return false;
          }
          let ix2 = ix2 + 1;
        }
        return true;
      }
      def by_length(s1, s2) {
        return s1.length > s2.length;
      }
      while (buffer.length != 0) {
        # do _print(_array("buffer", buffer));
        let valid_prefixes = ALL_SYMBOLS.filter(validity_filter).sort(by_length);
        if (valid_prefixes.length == 0) {
          do tokenize_error("cannot parse \""+buffer.join("")+"\" into symbols", pos);
        }

        let to_push = valid_prefixes.pop();
        do tokens.push(_array("SYMBOL", to_push, pos)); # @todo: fix pos; won't always be correct

        # buffer.shift(to_push.length)
        let ix2 = 0;
        while (ix2 < to_push.length) {
          do _leftpop(buffer);
          let ix2 = ix2 + 1;
        }
      }
    } else {
      do tokenize_error("unrecognized character: "+char, pos);
    }

    do next_char_();
  }
  if (logging) {
    do _print("tokens:");
    do _print(tokens);
  }
  return tokens;
}

def parse_entrypoint(tokens) {
  do tokens.reverse(); # convinient for next_token_ etc

  alloc ast;
  let ast = _array();
  while (peek(tokens)) { # @hack: while not undefined
    do ast.push(parse_("STATEMENT", tokens));
  }

  if (tokens.length != 0) {
    do parse_error("expected EOF", peek(tokens));
  }

  return ast;
}

def next_token_(tokens) {
  let tok = tokens.pop();
  if (tok) {
    #@hack around undefined
    return tok;
  } else {
    do parse_error("unexpected EOF", _array("EOF", "EOF", _array(-1, -1)));
  }
}

def peek(tokens) {
  return _get(tokens, tokens.length - 1);
}

def tok_match(tok, type, val) {
  return tok && _get(tok, 0) == type && _get(tok, 1) == val;
}

def parse_error(msg, tok) {
  let type = _get(tok, 0);
  let val = _get(tok, 1);
  let pos = _get(tok, 2);

  let line = _get(pos, 0);
  let char = _get(pos, 1);

  do _print("Parse error at "+line+":"+char+" (\""+val+"\"):\n\t"+msg);
  do _EXIT(1);
}

def parse_(state, tokens) {
  # do _print(_array("parse_", state, peek(tokens)));

  if (state == "SCOPE") {
    let tok = next_token_(tokens);
    if (false == tok_match(tok, "SYMBOL", "{")) {
      do parse_error("scope missing '{'", tok);
    }
    alloc ast;
    let ast = _array();
    while (true) {
      if (tok_match(peek(tokens), "SYMBOL", "}")) {
        do next_token_(tokens);
        return _array("SCOPE", ast);
      }
      do ast.push(parse_("STATEMENT", tokens));
    }
  } else if (state == "LET") {
    alloc target;
    let target = next_token_(tokens);
    if (false == (_get(target, 0) == "IDENTIFIER")) {
      do parse_error("bad let target", target);
    }

    let tok = next_token_(tokens);
    if (false == tok_match(tok, "SYMBOL", "=")) {
      do parse_error("missing '='", tok);
    }

    let rhs = parse_("EXPRESSION", tokens);

    let tok = next_token_(tokens);
    if (false == tok_match(tok, "SYMBOL", ";")) {
      do parse_error("missing ';'", tok);
    }

    return _array("LET", target, rhs);
  } else if (state == "EXPRESSION") {
    return _parse_expression_(tokens);
  } else if (state == "CALL") {
    alloc name_;
    let name_ = next_token_(tokens);
    if (_get(name_, 0) != "IDENTIFIER") {
      do parse_error("bad call name", name_);
    }

    let paren_tok = next_token_(tokens);
    if (false == tok_match(paren_tok, "SYMBOL", "(")) {
      do parse_error("missing '('", paren_tok);
    }

    let args = parse_("EXPLIST", tokens);

    let paren_tok = next_token_(tokens);
    if (false == tok_match(paren_tok, "SYMBOL", ")")) {
      do parse_error("missing ')'", paren_tok);
    }

    return _array("CALL", name_, args);
  } else if (state == "DO") {
    let expr = parse_("EXPRESSION", tokens);

    let semi_tok = next_token_(tokens);
    if (false == tok_match(semi_tok, "SYMBOL", ";")) {
      do parse_error("missing ';'", semi_tok);
    }

    return _array("DO", expr);
  } else if (state == "EXPLIST") {
    if (tok_match(peek(tokens), "SYMBOL", ")")) {
      return _array();
    } else {
      alloc buffer;
      let buffer = _array();
      while (true) {
        do buffer.push(parse_("EXPRESSION", tokens));
        let popped = next_token_(tokens);
        if (false == tok_match(popped, "SYMBOL", ",")) {
          do tokens.push(popped); # return the token
          return buffer;
        }
      }
    }
  } else if (state == "ARGLIST") {
    if (tok_match(peek(tokens), "SYMBOL", ")")) {
      return _array();
    } else {
      let buffer = _array();
      while (true) {
        let tok = next_token_(tokens);
        if (_get(tok, 0) != "IDENTIFIER") {
          do parse_error("bad arg", tok);
        }
        do buffer.push(tok);
        let popped = next_token_(tokens);
        if (false == tok_match(popped, "SYMBOL", ",")) {
          do tokens.push(popped); # return the token
          return buffer;
        }
      }
    }
  } else if (state == "DEF") {
    alloc name_;
    let name_ = next_token_(tokens);
    if (_get(name_, 0) != "IDENTIFIER") {
      do parse_error("bad function name", name_);
    }

    let paren_tok = next_token_(tokens);
    if (false == tok_match(paren_tok, "SYMBOL", "(")) {
      do parse_error("def '"+_get(name_, 1)+"' missing '('", paren_tok);
    }

    alloc params;
    let params = parse_("ARGLIST", tokens);

    let paren_tok = next_token_(tokens);
    if (false == tok_match(paren_tok, "SYMBOL", ")")) {
      do parse_error("def '"+_get(name_, 1)+"' missing ')'", paren_tok);
    }

    let body = parse_("SCOPE", tokens);
    return _array("DEF", name_, params, body);
  } else if (state == "IF") {
    alloc cond;
    alloc then_;
    alloc else_;

    let paren_tok = next_token_(tokens);
    if (false == tok_match(paren_tok, "SYMBOL", "(")) {
      do parse_error("'if' missing '('", paren_tok);
    }

    let cond = parse_("EXPRESSION", tokens);

    let paren_tok = next_token_(tokens);
    if (false == tok_match(paren_tok, "SYMBOL", ")")) {
      do parse_error("'if' missing ')'", paren_tok);
    }

    let then_ = parse_("SCOPE", tokens);

    if (tok_match(peek(tokens), "IDENTIFIER", "else")) {
      do next_token_(tokens); # consume 'else'
      if (tok_match(peek(tokens), "SYMBOL", "{")) {
        let else_ = parse_("SCOPE", tokens);
      } else if (tok_match(peek(tokens), "RESERVED", "if")) {
        do next_token_(tokens); # consume 'if'
        let else_ = parse_("IF", tokens);
      } else {
        do parse_error("bad 'else' branch", peek(tokens));
      }
    } # @hack: else_ will be undefined if this branch is not taken
    return _array("IF", cond, then_, else_);
  } else if (state == "WHILE") {
    alloc cond;
    alloc body;
    let cond = parse_("EXPRESSION", tokens);
    let body = parse_("SCOPE", tokens);
    return _array("WHILE", cond, body);
  } else if (state == "RETURN") {
    let val = parse_("EXPRESSION", tokens);
    let semi_tok = next_token_(tokens);
    if (false == tok_match(semi_tok, "SYMBOL", ";")) {
      do parse_error("missing ';'", semi_tok);
    }
    return _array("RETURN", val);
  } else if (state == "BREAK") {
    let semi_tok = next_token_(tokens);
    if (false == tok_match(semi_tok, "SYMBOL", ";")) {
      do parse_error("missing ';'", semi_tok);
    }
    return _array("BREAK");
  } else if (state == "RAISE") {
    let val = parse_("EXPRESSION", tokens);
    let semi_tok = next_token_(tokens);
    if (false == tok_match(semi_tok, "SYMBOL", ";")) {
      do parse_error("missing ';'", semi_tok);
    }
    return _array("RAISE", val);
  } else if (state == "ALLOC") {
    let target = next_token_(tokens);
    if (_get(target, 0) != "IDENTIFIER") {
      do parse_error("bad alloc target", target);
    }

    let semi_tok = next_token_(tokens);
    if (false == tok_match(semi_tok, "SYMBOL", ";")) {
      do parse_error("missing ';'", semi_tok);
    }

    return _array("ALLOC", target);
  } else if (state == "STATEMENT") {
    let tok = next_token_(tokens);
    let type = _get(tok, 0);
    let val = _get(tok, 1);
    if (type == "RESERVED") {
      if (STATEMENT_STARTERS.includes(val)) {
        return parse_(val.toUpperCase(), tokens);
      } else {
        raise "iae: unknown reserved word: "+_get(tok, 1);
      }
    } else {
      do parse_error("unexpected statement", tok);
    }
  } else {
    raise "iae: unimplemented state: " + state;
  }
}

def _parse_expression_(tokens) {
  # do _print(_array("_parse_expression_", peek(tokens)));

  alloc op;
  alloc op2;
  alloc prec;
  alloc prec2;
  alloc lhs;
  alloc rhs;

  let lhs = _parse_operand_(tokens);
  let op = next_token_(tokens);
  let prec = _precedence(_get(op, 1));
  if (prec == -1) {
    do tokens.push(op); # put it back
    return lhs;
  }

  while (true) {
    let rhs = _parse_operand_(tokens);
    let op2 = next_token_(tokens);
    let prec2 = _precedence(_get(op2, 1));

    if (prec2 == -1) {
      do tokens.push(op2); # put it back
      return _array("BINOP", op, lhs, rhs);
    }

    # do _print(_array("comparing", op, op2, prec, prec2));
    if (prec <= prec2) {
      let lhs = _array("BINOP", op, lhs, rhs);
      let op = op2;
      let prec = prec2;
    } else {
      do tokens.push(op2);
      do tokens.push(rhs);
      return _array("BINOP", op, lhs, _parse_expression_(tokens));
    }
  }
}

def _parse_operand_(tokens) {
  # do _print(_array("_parse_operand_", peek(tokens)));

  let tok = next_token_(tokens);
  let type = _get(tok, 0);
  let val = _get(tok, 1);
  let pos = _get(tok, 2);

  if (type == "STRING") {
    return tok;
  } else if (type == "INTEGER") {
    return tok;
  } else if (type == "RESERVED") {
    if (false == (val == "true" || val == "false")) {
      do parse_error("reserved word in expression", tok);
    }
    return tok;
  } else if (type == "IDENTIFIER") {
    if (tok_match(peek(tokens), "SYMBOL", "(")) {
      do tokens.push(tok); # put fxn name back
      return parse_("CALL", tokens);
    } else {
      return tok;
    }
  } else if (type == "SYMBOL") {
    if (tok_match(tok, "SYMBOL", "-")) {
      # @hack
      do tokens.push(tok); # put '-' back
      return _array("INTEGER", "0", pos);
    } else if (tok_match(tok, "SYMBOL", "(")) {
      let lhs = parse_("EXPRESSION", tokens);
      let paren_tok = next_token_(tokens);
      if (false == tok_match(paren_tok, "SYMBOL", ")")) {
        do parse_error("missing ')'", paren_tok);
      }
      return lhs;
    } else {
      do parse_error("bad lhs", tok);
    }
  } else if (type == "CALL") {
    # @hack this happens when a :call "token" is pushed back onto the tokens stack b/c of operator precedence
    return tok;
  } else {
    raise "iae: _parse_operand! type="+type;
  }
}

def _precedence(op_val) {
  let ix = BINOPS.indexOf(op_val);
  if (ix != -1) {
    return _get(BINOP_PREC, ix);
  } else {
    return -1;
  }
}

def generate_code(ast) {
  return ast.map(codegen).join("\n");
}

def codegen_list(list, sep) {
  let list = list.map(codegen);
  return list.join(sep);
}

def codegen(node) {
  # do _print(_array("codegen", node));

  let node_type = _leftpop(node);
  if (node_type == "SCOPE") {
    let body = _leftpop(node);

    return body.map(codegen).join("\n");
  } else if (node_type == "LET") {
    alloc target;
    alloc rhs;
    let target = _leftpop(node);
    let rhs = _leftpop(node);
    return codegen(target)+" = "+codegen(rhs)+";";
  } else if (node_type == "BINOP") {
    alloc lhs;
    alloc type;
    alloc rhs;
    let type = _leftpop(node);
    let lhs = _leftpop(node);
    let rhs = _leftpop(node);
    return "("+codegen(lhs)+" "+codegen(type)+" "+codegen(rhs)+")";
  } else if (node_type == "CALL") {
    alloc args;
    alloc name_str;
    let name_ = _leftpop(node);
    let args = _leftpop(node);
    let name_str = codegen(name_);
    if (name_str == "_array") { # shim in javascript methods
      return "["+codegen_list(args, ", ")+"]";
    } else if (name_str == "_leftpop") {
      let arr = _leftpop(args);
      return codegen(arr)+".splice(0,1)[0]";
    } else if (name_str == "_get") {
      alloc arr;
      alloc ix;
      let arr = _leftpop(args);
      let ix = _leftpop(args);
      return codegen(arr)+"["+codegen(ix)+"]";
    } else if (name_str == "_readFile") {
      let fname = _leftpop(args);
      return "require('fs').readFileSync("+codegen(fname)+", 'utf-8')";
    } else if (name_str == "_writeFile") {
      alloc fname;
      alloc data;
      let fname = _leftpop(args);
      let data = _leftpop(args);
      return "require('fs').writeFileSync("+codegen(fname)+", "+codegen(data)+", 'utf-8')";
    } else if (name_str == "_inspect") {
      let data = _leftpop(args);
      return "require('util').inspect("+codegen(data)+", false, null)";
    } else if (name_str == "_print") {
      let val = _leftpop(args);
      return "console.log("+codegen(val)+")";
    } else if (name_str == "_ARGV") {
      return "process.argv.slice(2)";
    } else if (name_str == "_EXIT") {
      let val = _leftpop(args);
      return "process.exit("+codegen(val)+")";
    } else {
      return name_str+"("+codegen_list(args, ", ")+")";
    }
  } else if (node_type == "DO") {
    let call = _leftpop(node);
    return codegen(call) + ";";
  } else if (node_type == "DEF") {
    alloc name_;
    alloc params;
    alloc body;

    let name_ = _leftpop(node);
    let params = _leftpop(node);
    let body = _leftpop(node);
    return codegen_def(name_, params, body);
  } else if (node_type == "IF") {
    alloc cond;
    alloc then_;
    alloc else_;
    alloc fst;
    alloc snd;

    let cond = _leftpop(node);
    let then_ = _leftpop(node);
    let else_ = _leftpop(node);
    let fst = "if ("+codegen(cond)+") {\n"+codegen(then_)+"\n}";
    if (else_) {
      let snd = " else {\n"+codegen(else_)+"\n}";
    } else {
      # @hack if else_ is undefined
      let snd = "";
    }
    return fst + snd;
  } else if (node_type == "WHILE") {
    alloc cond;
    alloc body;

    let cond = _leftpop(node);
    let body = _leftpop(node);
    return "while ("+codegen(cond)+") {\n"+codegen(body)+"\n}";
  } else if (node_type == "RETURN") {
    let val = _leftpop(node);
    return "return "+codegen(val)+";";
  } else if (node_type == "BREAK") {
    return "break;";
  } else if (node_type == "RAISE") {
    let val = _leftpop(node);
    return "throw new Error("+codegen(val)+");";
  } else if (node_type == "ALLOC") {
    let target = _leftpop(node);
    return "let "+codegen(target)+";";
  } else if (node_type == "INTEGER") {
    let val = _leftpop(node);
    let pos = _leftpop(node);
    return "" + val;
  } else if (node_type == "STRING") {
    let val = _leftpop(node);
    let pos = _leftpop(node);
    return "\""+string_escape(val)+"\"";
  } else if (node_type == "RESERVED") {
    let word = _leftpop(node);
    let pos = _leftpop(node);
    return word;
  } else if (node_type == "SYMBOL") {
    let val = _leftpop(node);
    let pos = _leftpop(node);
    if (val == "==") {
      let val = "===";
    }
    return val;
  } else if (node_type == "IDENTIFIER") {
    let varname = _leftpop(node);
    let pos = _leftpop(node);
    return varname;
  } else {
    raise "iae: unrecognized node type: " + node_type;
  }
}

def codegen_def(name_, params, body) {
  return "function "+codegen(name_)+"("+codegen_list(params, ", ")+") {\n"+codegen(body)+"\n}";
}

def generate_escape_code(char) {
  if (char == "n") {
    return "\n";
  } else if (char == "t") {
    return "\t";
  } else {
    return char;
  }
}

def string_escape(str) {
  def helper(char) {
    if (char == "\n") {
      return "\\n";
    } else if (char == "\t") {
      return "\\t";
    } else if (char == "\\") {
      return "\\\\";
    } else if (char == "\"") {
      return "\\\"";
    } else {
      return char;
    }
  }

  let arr = str.split("");
  let arr = arr.map(helper);
  return arr.join("");
}

def _tok_to_string(tok) {
  let type = _get(tok, 0);
  let val = _get(tok, 1);
  let pos = _get(tok, 2);
  let val_to_print = string_escape(val);

  return "[ :" + type + ", \"" + val_to_print + "\" ],";
}

def _tokens_to_string(tokens) {
  let buf = _array("[");
  let buf = buf.concat(tokens.map(_tok_to_string));
  do buf.push("]");
  return buf.join("\n");
}

let _ = _ARGV();
let infile = _get(_, 0);
let outfile = _get(_, 1);
let text = _readFile(infile);
let tokens = tokenize(text, false);
do _writeFile("ahuff/boot_tokens.txt", _tokens_to_string(tokens));
let ast = parse_entrypoint(tokens);
do _writeFile("ahuff/boot_ast.txt", _inspect(ast));
let code = generate_code(ast);
do _writeFile(outfile, code);
